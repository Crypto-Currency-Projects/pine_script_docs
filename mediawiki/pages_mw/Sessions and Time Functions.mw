
__FORCETOC__

== Functions and the Variable <code>time</code> ==
In Pine there are special means for working with trade sessions, time and date. We will review a simple chart, IBM,30 on which has been applied 2 scripts: “Bar date/time” and “Session bars”.

[[File:Chart time 1.png]]

Here is the initial code of the first script "Bar date/time":
 <source>study("Bar date/time")
plot(time)</source>

This illustrates the meaning of the variable time. The variable [https://www.tradingview.com/study-script-reference/#var_time <code>time</code>] returns the date/time (timestamp) of each bar on the chart in UNIX format. As can be seen from the screenshot, the value <code>time</code> on the last bar is equal to 1397593800000. This value is the number of milliseconds that have passed since 00:00:00 UTC, 1 January, 1970 and corresponds to Tuesday, 15th of April, 2014 at 20:30:00 UTC. (There are a lot of online convertors, for example [http://www.onlineconversion.com/unix_time.htm OnlineConversion.com]). The chart’s time gauge in the screenshot shows the time of the last bar as 2014-04-15 16:30 (in the exchange timezone, from here the difference between this time and UTC is 4 hours).

The second script, "Session bars":

 <source>study("Session bars")
t = time(period, "0930-1600")
plot(na(t) ? 0 : 1)</source>


This shows how the user can distinguish between session bars and bars that get into extended hours by using the built-in function <code>time</code> and not the variable <code>time</code> (the background behind these bars has been colored over with grey). The function <code>time</code> returns the time of the bar in milliseconds UNIX time or NaN value if the bar is located outside the given trade session (09:30-16:00 in our example). <code>time</code> accepts two arguments, the first is ‘resolution’, the bars of which are needed to determine their timestamp, and the second — ‘session specification’, which is a string that specifies the beginning and end of the trade session (in the exchange timezone). The string “0930-1600” corresponds to the trade session symbol IBM. Examples of trade session configurations:
“0000-0000” — a complete 24 hours with the session beginning at midnight.
“1700-1700” — a complete 24 hours with the session beginning at 17:00.
"0900-1600,1700-2000" — a session that begins at 9:00 with a break at 16:00 until 17:00 and ending at 20:00
"2000-1630" — an overnight session that begins at 20:00 and ends at 16:30 the next day.
“0930-1700:146” - a session that begins at 9:30 and ends at 17:00 on Sundays (1), Wednesdays (4) and Fridays (6) (other days of the week are days off).

Session specification, which is being passed to the function <code>time</code>, is not required to correspond with the real trade session of the symbol on the chart. It’s possible to pass different “hypothetical” session specifications which can be used to highlight those or (other?) bars in a data series. It’s possible to transfer the different ‘hypothetical’ session specifications which can be used to highlight those or other bars in a data series.

There is an overloaded function <code>time</code> that allows the user to skip custom session specification. In this case, internally, it will use a regular session specification of a symbol. For example, it’s possible to highlight the beginning of each half-hour bar on a minute-based chart in the following way:

 <source>study("new 30 min bar")
is_newbar(res) =>
    t = time(res)
    change(t) != 0 ? 1 : 0
plot(is_newbar("30"))</source>

[[File:Chart time 2.png]]

The function <code>is_newbar</code> similar to the previous example can be used in many situations. For example, it’s essential to display on an intraday chart the highs and lows which began at the market’s opening:

<source>//@version=3
study("Opening high/low", overlay=true)

highTimeFrame = input("D", type=resolution)
sessSpec = input("0930-1600", type=session)

is_newbar(res, sess) =>
    t = time(res, sess)
    na(t[1]) and not na(t) or t[1] < t

newbar = is_newbar("1440", sessSpec)
s1 = na
s1 := newbar ? low : nz(s1[1])
s2 = na
s2 := newbar ? high : nz(s2[1])

plot(s1, style=circles, linewidth=3, color=red)
plot(s2, style=circles, linewidth=3, color=lime)</source>


[[File:Chart time 3.png]]

Pay attention to the variables <code>highTimeFrame</code> and <code>sessSpec</code>. They have been declared in a special way with the variable of the functions <code>input</code>. Further information about indicator's inputs can be found here: [http:////www.tradingview.com/study-script-reference/#fun_input input variables].

== Session Format by example ==

* <code>24x7</code> - is everyday session 00:00 - 00:00.
* <code>0000-0000:1234567</code> - same as <code>24x7</code>.
* <code>0000-0000:23456</code> - same as <code>0000-0000</code>, monday to friday session that starts every day at 00:00 and ends at 00:00 of the next day.
* <code>1700-1700</code> - is an overnight session. Monday session starts at sunday, 17:00, and ends at monday, 17:00. Also, only on monday-friday.
* <code>1000-1001:26</code> - is a weird session, that lasts only one minute on mondays, and one minute on fridays.

== Built-in Variables for working with Time ==
Pine’s standard library has an assortment of built-in variables which allow a bar’s time in the logic of an argument’s algorithm to be used in scripts:
* <code>time</code> — UNIX time of the current bar in milliseconds '''(in UTC timezone)'''.
* <code>year</code> — Current bar year.
* <code>month</code> — Current bar month.
* <code>weekofyear</code> — Week number of current bar time.
* <code>dayofmonth</code> — Date of current bar time.
* <code>dayofweek</code> — Day of week for current bar time. You can use [https://www.tradingview.com/study-script-reference/#var_sunday sunday], [https://www.tradingview.com/study-script-reference/#var_monday monday], [https://www.tradingview.com/study-script-reference/#var_tuesday tuesday], [https://www.tradingview.com/study-script-reference/#var_wednesday wednesday], [https://www.tradingview.com/study-script-reference/#var_thursday thursday], [https://www.tradingview.com/study-script-reference/#var_friday friday] and [https://www.tradingview.com/study-script-reference/#var_saturday saturday] variables for comparisons.
* <code>hour</code> — Current bar hour.
* <code>minute</code> — Current bar minute.
* <code>second</code> — Current bar second.

The following are also built-in functions:
* <code>year(x)</code> — Returns year for provided UTC time.
* <code>month(x)</code> — Returns month for provided UTC time.
* <code>weekofyear(x)</code> — Returns week of year for provided UTC time.
* <code>dayofmonth(x)</code> — Returns day of month for provided UTC time.
* <code>dayofweek(x)</code> — Returns day of week for provided UTC time.
* <code>hour(x)</code> — Returns hour for provided UTC time.
* <code>minute(x)</code> — Returns minute for provided UTC time.
* <code>second(x)</code> — Returns second for provided time.

All these variables and functions return '''time in exchange time zone''', except for the <code>time</code> variable which returns time in UTC timezone.

----
Previous: [[Bar states. Built-in variables ‘barstate’]], Next: [[Extended_and_Regular_Sessions]], Up: [[Pine Script Tutorial]]
[[Category:Pine Script]]